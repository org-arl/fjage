apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

defaultTasks 'jars'

archivesBaseName = 'fjage'
group = 'com.github.org-arl'
version = new File('VERSION').text.trim()

targetCompatibility = 1.8
sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.5.8'
  compile 'org.jline:jline:3.12.1'
  compile 'org.apache.commons:commons-lang3:3.9'
  compile 'commons-io:commons-io:2.6'
  compile 'uk.com.robust-it:cloning:1.9.12'
  compile 'org.eclipse.jetty:jetty-server:9.4.20.v20190813'
  compile 'org.eclipse.jetty:jetty-servlet:9.4.20.v20190813'
  compile 'org.eclipse.jetty.websocket:websocket-server:9.4.20.v20190813'
  compile 'com.google.code.gson:gson:2.8.5'
  compile 'com.fazecast:jSerialComm:2.5.1'
  testCompile 'junit:junit:4.12'
}

compileJava {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-options"
}

compileTestJava {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-options"
}

compileGroovy {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-options"
}

test {
  systemProperties project.properties.subMap(["manualJSTest"])
  systemProperties project.properties.subMap(["manualPyTest"])
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
}

jar {
  manifest {
    attributes 'Build-Owner': System.getenv().USER, 'Build-Timestamp': new Date().format('d-MM-yyyy_HH:mm:ss'), 'Build-Version': version
  }
}

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
  }
}

task jars(dependsOn: jar, type: Copy) {
  into "$buildDir/libs"
  from configurations.runtime
}

jars.outputs.upToDateWhen { false }

////// tasks for MavenCentral deployment

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment {
        MavenDeployment deployment -> signing.signPom(deployment)
      }
      if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
      }
      pom.project {
        name archivesBaseName
        packaging 'jar'
        description 'Framework for Java and Groovy Agents'
        url 'http://github.com/org-arl/fjage'
        scm {
          url 'http://github.com/org-arl/fjage'
          connection 'scm:git:git://github.com/org-arl/fjage.git'
          developerConnection 'scm:git:git@github.com:org-arl/fjage.git'
        }
        licenses {
          license {
            name '3-clause BSD License'
            url 'http://github.com/org-arl/fjage/blob/master/LICENSE.txt'
            distribution 'repo'
          }
        }
        developers {
          developer {
            id 'mchitre'
            name 'Mandar Chitre'
          }
        }
      }
    }
  }
}
