plugins {
    id 'groovy'
    id 'eclipse'
    id 'maven'
    id 'signing'
    id 'com.adarshr.test-logger' version '2.1.1'
}

defaultTasks 'jars'

archivesBaseName = 'fjage'
group = 'com.github.org-arl'
version = new File('VERSION').text.trim()

// get git commit
def stdout = new ByteArrayOutputStream()
exec {
  commandLine 'git', 'describe', '--always', '--abbrev=8', '--match', 'NOT A TAG', '--dirty=*'
  standardOutput = stdout;
}
def commit = "$stdout".replace('\n','')

targetCompatibility = 1.8
sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.codehaus.groovy:groovy:2.5.9'
  implementation 'org.jline:jline:3.12.1'
  implementation 'org.apache.commons:commons-lang3:3.9'
  implementation 'commons-io:commons-io:2.6'
  implementation 'uk.com.robust-it:cloning:1.9.12'
  implementation 'org.eclipse.jetty:jetty-server:9.4.26.v20200117'
  implementation 'org.eclipse.jetty:jetty-servlet:9.4.26.v20200117'
  implementation 'org.eclipse.jetty.websocket:websocket-server:9.4.26.v20200117'
  implementation 'com.google.code.gson:gson:2.8.5'
  implementation 'com.fazecast:jSerialComm:2.5.1'
  testImplementation 'junit:junit:4.12'
  testImplementation 'net.jodah:concurrentunit:0.4.6'
}

compileJava {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-options"
}

compileTestJava {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-options"
}

compileGroovy {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-options"
}

test {
  systemProperty "random.testing.seed", new Random().nextInt()
  systemProperties project.properties.subMap(["manualJSTest"])
  systemProperties project.properties.subMap(["manualPyTest"])
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
}

jar {
  manifest {
    attributes 'Build-Owner': System.getenv().USER, 'Build-Timestamp': new Date().format('d-MM-yyyy_HH:mm:ss'), 'Build-Version': "${project.version}/${commit}"
  }
}

testlogger {
  showFullStackTraces false
  showStandardStreams true
  showPassedStandardStreams false
  showSkippedStandardStreams false
  showFailedStandardStreams true
}


task jars(dependsOn: jar, type: Copy) {
  into "$buildDir/libs"
  from configurations.runtimeClasspath
}

jars.outputs.upToDateWhen { false }

javadoc.doLast {
  mkdir 'docs/javadoc'
  copy {
    from javadoc.destinationDir
    into 'docs/javadoc'
  }
}

////// tasks for MavenCentral deployment

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment {
        MavenDeployment deployment -> signing.signPom(deployment)
      }
      if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
      }
      pom.project {
        name archivesBaseName
        packaging 'jar'
        description 'Framework for Java and Groovy Agents'
        url 'http://github.com/org-arl/fjage'
        scm {
          url 'http://github.com/org-arl/fjage'
          connection 'scm:git:git://github.com/org-arl/fjage.git'
          developerConnection 'scm:git:git@github.com:org-arl/fjage.git'
        }
        licenses {
          license {
            name '3-clause BSD License'
            url 'http://github.com/org-arl/fjage/blob/master/LICENSE.txt'
            distribution 'repo'
          }
        }
        developers {
          developer {
            id 'mchitre'
            name 'Mandar Chitre'
          }
        }
      }
    }
  }
}
