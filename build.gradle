apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

defaultTasks 'jars'

archivesBaseName = 'fjage'
group = 'com.github.org-arl'
version = new File('VERSION').text.trim()
docsDirName = '../htdocs'

targetCompatibility = 1.8
sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.4.4'
  compile 'jline:jline:2.12'
  compile 'org.apache.commons:commons-lang3:3.1'
  compile 'uk.com.robust-it:cloning:1.9.0'
  compile 'org.eclipse.jetty:jetty-server:8.0.4.v20111024'
  compile 'org.eclipse.jetty:jetty-servlet:8.0.4.v20111024'
  compile 'org.eclipse.jetty:jetty-eventsource-servlet:1.0.0'
  compile 'com.google.code.gson:gson:2.8.2'
  compile 'com.fazecast:jSerialComm:2.1.1'
  testCompile 'junit:junit:4.11'
}

compileJava {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-options"
}

compileTestJava {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-options"
}

compileGroovy {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-options"
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
}

jar {
  manifest {
    attributes 'Build-Owner': System.getenv().USER, 'Build-Timestamp': new Date().format('d-MM-yyyy_HH:mm:ss'), 'Build-Version': version
  }
}

task jars(dependsOn: jar, type: Copy) {
  into "$buildDir/libs"
  from configurations.runtime
}

jars.outputs.upToDateWhen { false }

task doc {
  doLast {
    ant.exec(executable: 'make', dir: '.', failonerror: true) {
      arg(line: 'html')
    }
  }
}

////// tasks for MavenCentral deployment

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment {
        MavenDeployment deployment -> signing.signPom(deployment)
      }
      if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
      }
      pom.project {
        name archivesBaseName
        packaging 'jar'
        description 'Framework for Java and Groovy Agents'
        url 'http://github.com/org-arl/fjage'
        scm {
          url 'http://github.com/org-arl/fjage'
          connection 'scm:git:git://github.com/org-arl/fjage.git'
          developerConnection 'scm:git:git@github.com:org-arl/fjage.git'
        }
        licenses {
          license {
            name '3-clause BSD License'
            url 'http://github.com/org-arl/fjage/blob/master/LICENSE.txt'
            distribution 'repo'
          }
        }
        developers {
          developer {
            id 'mchitre'
            name 'Mandar Chitre'
          }
        }
      }
    }
  }
}
